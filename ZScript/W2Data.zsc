// Weathremum general data classes & constants.

// AA: 0x7FAA0004.
// ZC: 0x7FAA0005 + 0x7FAA0006.
const WeathremumHandlerOrder = 0x7FAA0007;
const STAT_WEATHREMUM = Thinker.STAT_USER + 5;


enum EWeathremumHandleStatus {
	W2HS_Off = 0,		// Stopped, inactive sector sound.
	W2HS_Active,		// Active sector sound.
	W2HS_StopSuggestion	// Pre-stop (must be changed to something else every "WorldTick()").
}

enum EWeathremumSectorType {
	W2ST_None = 0,
	W2ST_Outdoor,
	W2ST_NextToOutdoor
}


class W2LevelGeneralData {
	Array<W2SectorData> allSectorsData;		// All of the sectors.

	Array<W2SectorGroup> allSectorGroups;	// Megasectors: uniting all neighbour sectors with same "w2type" to same unique "groupnum".
	Array<W2SectorGroup> realSectorGroups;	// Megasectors: same as above, but only for existing groups.

	Array<int> corniceLinesFlags;			// For the raindrops effect from a canopy.
}


class W2SectorData {
	Sector s;
	Array<Line> lines;
	//Array<int> lineindices; // Temporary, for the UI-scoped "RenderOverlay()".

	int groupnum;
	W2SectorGroup group;
	EWeathremumSectorType w2type;

	static W2SectorData Create( Sector initSector ) {
		W2SectorData newdata = new( 'W2SectorData' );
		newdata.s = initSector;
		newdata.groupnum = -1;

		return newdata;
	}
}

// Megasectors/sectors union.
class W2SectorGroup {
	int num;

	EWeathremumHandleStatus status;
	W2SoundOrigin soundorigin;

	Array<Sector> sectors;
	Array<Line> lines;		// A perimeter lines.

	static W2SectorGroup Create( int initnum = -1 ) {
		W2SectorGroup newdata = new( 'W2SectorGroup' );
		newdata.num = initnum;

		return newdata;
	}
}

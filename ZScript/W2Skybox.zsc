// All of the Weathremum skybox stuff.

class W2BaseSkyActor: Actor {
	W2SkyboxViewpoint skycam;
	vector3 sphericaloffset; //double ra, decl, dist;

	Default {
		+NOINTERACTION;
		+NOTONAUTOMAP;
		+BRIGHT;
	}

	static clearscope vector3 vector3ToSpherical( vector3 vec ) {
		return (atan2( vec.y, vec.x ), -90.0 * sin( vec.z ), vec.length());
	}

	static clearscope vector3 sphericalToVector3( vector3 spherical ) {
		double cosangle = cos( spherical.x );
		double cospitch = cos( spherical.y );

		return (cosangle * cospitch, sin( spherical.x ) * cospitch, sin( spherical.y )) * spherical.z;
	}

	void AddVec3Pos( vector3 vec3pos ) {
		sphericaloffset = vector3ToSpherical( sphericalToVector3( sphericaloffset ) + vec3pos );
	}

	/*static double, double vector3ToAnglePitch( vector3 vec ) {
		return atan2( vec.y, vec.x ), -90.0 * sin( vec.z );
	}

	static vector3 anglePitchToVector3( double angle, double pitch ) {
		double cosangle = cos( angle );
		double cospitch = cos( pitch );

		return (cosangle * cospitch, sin( angle ) * cospitch, sin( pitch ));
	}*/

} // of class W2BaseSkyActor: Actor {}

class W2StaticSkyActor: W2BaseSkyActor {
	States {
	Spawn:
		TROO A -1;
		Stop;
	}
} // of class W2StaticSkyActor: W2BaseSkyActor {}

class W2DynamicSkyActor: W2BaseSkyActor {
	Default {
		RenderStyle "Translucent";
	}

	override void Tick() {
		SetOrigin( skycam.pos + sphericalToVector3( sphericaloffset ), true );
		Super.Tick();
	}
} // of class W2DynamicSkyActor: W2BaseSkyActor {}


class W2SkyboxViewpoint: SkyViewpoint {
	Array<W2BaseSkyActor> skyboxActors;

	double realangle;

	static W2SkyboxViewpoint Create( int playernum ) {
		W2SkyboxViewpoint newskies = W2SkyboxViewpoint( Actor.Spawn( "W2SkyboxViewpoint", (0, 0, 0) ) );

		newskies.FriendPlayer = clamp( playernum, 0, MAXPLAYERS );
		newskies.master = ( playeringame[ playernum ] && players[ playernum ].mo )? players[ playernum ].mo : NULL;
		newskies.SetOrigin( (-100000, 0, 0), false );

		newskies.FindFloorCeiling();
		newskies.SetZ( newskies.floorz - 16.0 );

		newskies.bNOINTERACTION = true;
		newskies.UnlinkFromWorld();

		if ( newskies.master )
			newskies.realangle = -newskies.master.angle;

		return newskies;
	}

	override void Tick() {
		if ( !master ) {
			W2Global.Log( LL_Detailed, GetClassName() .. "::Tick(). No master, destroying self." );
			Destroy();
		} else {
			// === For Doom 2, this works fine for all levels with skies, 
			//except MAP23 and MAP29. Only one side is visible:
			double prevangle = realangle;
			realangle -= master.player.cmd.yaw * ( 360.0 / 65536.0 );

			//angle = realangle;
			//ClearInterpolation();

			/*for ( int i = 0; i < skyboxActors.Size(); i++ ) {
				W2BaseSkyActor curactor = skyboxActors[ i ];
				curactor.sphericaloffset.x = ( curactor.sphericaloffset.x + realangle - prevangle ) % 360.0;
			}*/
		}
	} // of override void Tick() {}

	W2BaseSkyActor, uint CreateActor( class<W2BaseSkyActor> cls, vector3 thingCartesianOffset ) {
		W2BaseSkyActor skyactor = W2BaseSkyActor( Actor.Spawn( cls, pos + thingCartesianOffset ) );
		skyactor.skycam = self;
		skyactor.sphericaloffset = W2BaseSkyActor.vector3ToSpherical( thingCartesianOffset );

		return skyactor, skyboxActors.Push( skyactor );
	}

	W2BaseSkyActor, uint CreateActorSpherical( class<W2BaseSkyActor> cls, vector3 thingSphericalOffset ) {
		W2BaseSkyActor skyactor = W2BaseSkyActor( Actor.Spawn( cls, pos + W2BaseSkyActor.sphericalToVector3( thingSphericalOffset ) ) );
		skyactor.skycam = self;
		skyactor.sphericaloffset = thingSphericalOffset;

		return skyactor, skyboxActors.Push( skyactor );
	}

	void Clear() {
		for ( int i = 0; i < skyboxActors.Size(); i++ )
			skyboxActors[ i ].Destroy();

		Super.OnDestroy();
	}

	override void OnDestroy() {
		LinkToWorld();
		Super.OnDestroy();
	}
} // of class W2SkyboxViewpoint: SkyViewpoint {}


class W2SkyboxFog: W2DynamicSkyActor {
	enum ESkyboxFogType {
		SFT_Mist 	= 0,
		SFT_Light 	= 1,
		SFT_Dark 	= 2,
		SFT_Sun		= 3
	};

	Default {
		RenderStyle "Translucent";
		Alpha 0.1;
	}

	void SetType( ESkyboxFogType newtype, double newalpha = -0.1 ) {
		frame = newtype;

		if ( newalpha >= 0.0 )
			alpha = min( newalpha, 1.0 );
	}

	/*override void Tick() {
		//sphericaloffset.x += 1.0;
		//sphericaloffset.y += sin( level.maptime / 70.0 );
		//AddVec3Pos( ( sin( level.maptime / 1000.0 ) * 20.0, 0, 0 ) );
		Super.Tick();
	}*/

	States {
	Spawn:
		TROO A -1;
		Stop;
	}
} // of class W2SkyboxFog: W2DynamicSkyActor {}


class W2NightOceanSkybox: W2StaticSkyActor {
}

class W2Galaxy1Skybox: W2StaticSkyActor {
}


class W2SkyboxSmallStar: W2DynamicSkyActor {
	Default {
		+FORCEXYBILLBOARD;
	}

	int delayModulo;
	double theta, thetaGradient;

	override void PostBeginPlay() {
		Super.PostBeginPlay();
		scale.x = scale.y = FRandom( 0.2, 1.5 );

		delayModulo = Random( 5, 35 );
		theta = FRandom( 0.0, 360.0 );
		thetaGradient = FRandom( 1.0, 15.0 );
	}

	override void Tick() {
		if ( !( level.time % delayModulo ) ) {
			theta += thetaGradient;
			alpha = clamp( sin( theta ), 0.05, 1.0 );
		}

		//Super.Tick();
	}

	States {
	Spawn:
		STAR "[" -1;
		Stop;
	}
}

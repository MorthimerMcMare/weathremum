// Weathremum visual & audial ambience actor classes.

// A base weather sound actor.
class W2SoundOrigin: Actor {
	SeqNode seq;
	//W2SoundKeeper keeper;

	Default {
		+NOINTERACTION; // Implies +NOGRAVITY and +NOBLOCKMAP.
		+DONTSPLASH;
		+THRUACTORS;
		//+NOTONAUTOMAP;
		FloatBobPhase 0; // No more "FRandom pr_spawnmobj" desynchronizations!
	}

	override void Tick() {
		// No "Super.Tick()" is necessary.
	}

	override void OnDestroy() {
		StopSoundSequence();
		Super.OnDestroy();
	}
}


class Weathremum_Raindrop: Actor {
	W2LevelGeneralData w2level;
	W2SectorData w2sector;
	WeathremumUnsyncRandom rnd;

	Default {
		+MISSILE;
		-SOLID;
		+NOBLOCKMAP;
		+THRUACTORS; // No more "FRandom pr_reflect" ("Actor.BlockingMobj"/"AActor::AdjustReflectionAngle()") desynchronizations!
		+NOTONAUTOMAP;
		+DONTSPLASH;
		+NOTELEPORT;
		//+DONTOVERLAP;
		Height 0;
		Radius 1;
		Mass 3;
		RenderStyle "Translucent";
		FloatBobPhase 0; // No more "FRandom pr_spawnmobj" desynchronizations!
		DamageFunction 0; // No more "FRandom pr_damagemobj" desynchronizations!
	}

	override void BeginPlay() {
		ChangeStatNum( STAT_WEATHREMUM_MAINDECOR ); // 75.
		Super.BeginPlay();
	}

	override void PostBeginPlay() {
		//if ( !cursector || ( ceilingpic != skyflatnum ) )
		//	Destroy();

		double minspawnheight = ( master? min( ceilingz, target.pos.z + target.height + 192.0 ) : ceilingz );

		SetZ( minspawnheight - rnd.FRandom( 8.0, 25.0 ) );

		vel.z = -35.0;
		scale = ( rnd.FRandom( 0.1, 0.3 ), rnd.FRandom( 0.75, 2.0 ) );
		alpha = rnd.FRandom( 0.2, 0.7 );

		ClearInterpolation();
	}

	override void Tick() {
		if ( master && pos.z - master.pos.z < -256.0 ) {
			Destroy();
			return;
		}

		//if ( BlockingMobj )
		//	console.printf( "[Time " .. level.maptime .. "]. " .. GetClassName() .. "::Tick(). <" .. self .. "> BlockingMobj: " .. BlockingMobj );

		Super.Tick();
	}

	void RaindropPreSplash( void ) {
		w2sector = w2level.GetSectorData( Sector.PointInSector( pos.xy ) );
		EWeathremumSectorType w2type = ( w2sector? w2sector.w2type : W2ST_None );

		if ( w2sector ) {
			if ( w2sector.w2type == W2ST_Water ) {
				if ( target && Distance2D( target ) <= 512.0 ) {
					for ( int i = 0; i < 3; i++ ) {
						Weathremum_ExtraDecoration newdecor = Weathremum_ExtraDecoration( Actor.Spawn( "Weathremum_WaterRipple", pos + ( rnd.FRandom( -4.0, 4.0 ), rnd.FRandom( -4.0, 4.0 ), 0.0 ) ) );
						newdecor.rnd = rnd;
					}
				}
				Destroy();
				return;

			} else if ( ( w2sector.w2type == W2ST_Incandecent || w2sector.w2type == W2ST_Nukage ) && rnd.Chance( 7 ) ) {
				if ( w2sector.w2type == W2ST_Incandecent || rnd.Chance( 3 ) ) {
					Weathremum_ExtraDecoration newdecor = Weathremum_ExtraDecoration( Actor.Spawn( "Weathremum_NukageVapor", pos + ( 0.0, 0.0, 5.0 ) ) );
					newdecor.rnd = rnd;
				}

				Destroy();
				return;
			}
		}

		vel = (0, 0, 0);
		scale.y = 0.75;
		alpha *= 0.4;

		frame = rnd.Random( 1, 2 );
		//tics = frame; //rnd.Random( 2, 3 );

		SetZ( floorz );
		ClearInterpolation();

		bCORPSE = true;

		// Optimizing out raindrop splashes on the static surfaces:
		if ( ( cursector.floordata is 'Mover' ) )
			bMOVEWITHSECTOR = true;
	}

	void RaindropSplash( void ) {
		if ( w2sector && ( w2sector.w2type == W2ST_RockSurface || w2sector.w2type == W2ST_MetalSurface ) ) {
			Weathremum_ExtraDecoration newdecor = Weathremum_ExtraDecoration( Actor.Spawn( "Weathremum_HardSurfaceRaindropBreak", pos + ( rnd.FRandom( -4.0, 4.0 ), rnd.FRandom( -4.0, 4.0 ), 0.0 ) ) );
			newdecor.rnd = rnd;

			Destroy();
			return;
		}

		//ChangeStatNum( STAT_WEATHREMUM_EXTRADECOR ); // Not works.
		vel = (0.0, 0.0, 0.0);

		bFLATSPRITE = bROLLSPRITE = bFLOORCLIP = true;

		// General visual tweaks:
		ClearInterpolation();
		A_SetRenderStyle( 0.1, STYLE_Add );
		scale = ( 1, 1 ) * rnd.FRandom( 0.1, 0.25 );
		frame = rnd.Random( 0, 4 );

		// Setting the position on a slopes:
		if ( cursector.floorplane.isSlope() ) {
			vector3 norm = cursector.floorplane.normal;
			vector2 norm_p1 = ( norm.x != 0 || norm.y != 0 )? (norm.x, norm.y).Unit() : (0, 0);
			vector2 norm_p2 = ((norm.x, norm.y).Length(), norm.z);

			angle = atan2( norm_p1.y, norm_p1.x );
			pitch = atan2( norm_p2.x, norm_p2.y );
		}

		roll = rnd.FRandom( 0.0, 360.0 );
	}

	States {
	Spawn:
		RAIN N -1;
		Stop;
	Death:
		RAIN "#" 0 RaindropPreSplash();
		RAIN "#" 2 { frame++; tics = 5 - frame; }
		RAIN "#" 2 { frame++; }
		RWIN "#" 0 {
			if ( ( level.maptime & 1 ) || ( target && Distance2D( target ) > 512.0 ) )
				Destroy();
			else
				RaindropSplash();
		}
		RWIN "#" 1 A_FadeOut( 0.005 );
		Wait;
	}
} // of class Weathremum_Raindrop: Actor {}

class Weathremum_CanopyRaindrop: Weathremum_Raindrop {
	int dropCountdown;

	override void PostBeginPlay() {
		vel.z = 0.0;

		scale = ( rnd.FRandom( 0.1, 0.5 ), rnd.FRandom( 0.2, 0.5 ) );
		alpha = rnd.FRandom( 0.3, 0.7 );
		dropCountdown = rnd.Random( 1, 3 );
	}

	override void Tick() {
		if ( !bCORPSE && !globalfreeze && !level.frozen ) {
			if ( dropCountdown > 0 ) {
				dropCountdown--;

				scale.y *= 1.02;
			} else {
				if ( vel.z > -35.0 )
					vel.z -= 1.5;

				scale.y *= 1.1;
				vel.xy *= 0.9;
			}

			if ( scale.x > 0.2 )
				scale.x *= 0.95;
		}

		Super.Tick();
	}

	States {
	Death:
		RAIN "#" 1 RaindropPreSplash();
		RAIN "##" 1 { frame++; }
		RWIN "#" 0 {
			if ( target && Distance2D( target ) > 512.0 )
				Destroy();
			else
				RaindropSplash();
		}
		RWIN "#" 1 A_FadeOut( 0.005 );
		Wait;
	}
}


class Weathremum_ExtraDecoration: Actor {
	WeathremumUnsyncRandom rnd;

	Default {
		+NOTONAUTOMAP;
		+NOINTERACTION;
		+FLATSPRITE;
		+FLOORCLIP;
		RenderStyle "Translucent";
		FloatBobPhase 0; // No more "FRandom pr_spawnmobj" desynchronizations!
	}

	override void BeginPlay() {
		ChangeStatNum( STAT_WEATHREMUM_EXTRADECOR );
		//rnd = WeathremumUnsyncRandom.Get();
		Super.BeginPlay();
	}
}

class Weathremum_WaterRipple: Weathremum_ExtraDecoration {
	override void PostBeginPlay() {
		frame = rnd.Random( 0, 3 );
		alpha = rnd.FRandom( 0.095, 0.333 );
		scale.x = scale.y = rnd.FRandom( 0.1, 0.25 );
	}

	override void Tick() {
		if ( !globalfreeze && !level.frozen ) {
			Super.Tick();

			scale.y = ( scale.x += 0.025 );
			A_FadeOut( 0.025 );
		}
	}

	States {
	Spawn:
		WRPL "#" 2 {
			frame = ( frame + 1 ) % 4;
		}
		Loop;
	}
}

class Weathremum_HardSurfaceRaindropBreak: Weathremum_ExtraDecoration {
	override void PostBeginPlay() {
		frame = rnd.Random( 0, 3 );
		alpha = rnd.FRandom( 0.5, 1.0 );
		scale.x = scale.y = rnd.FRandom( 0.175, 0.45 );
	}

	override void Tick() {
		if ( !globalfreeze && !level.frozen ) {
			frame = ( frame + 1 ) % 4;
			A_FadeOut( 0.175 );
		}
	}

	States {
	Spawn:
		WRPL "#" -1;
		Stop;
	}	
}

class Weathremum_NukageVapor: Weathremum_ExtraDecoration {
	Default {
		-FLOORCLIP;
		RenderStyle "Add";
	}

	override void PostBeginPlay() {
		vel.z = rnd.FRandom( 0.0, 5.0 );
		alpha = rnd.FRandom( 0.075, 0.33 );

		scale.x = rnd.FRandom( 0.25, 0.75 );
		if ( rnd.Chance( 1 ) )
			scale.x = -scale.x;

		scale.y = scale.x;
	}

	States {
	Spawn:
		RAIS ABCDEFGHI 2 A_FadeOut( 0.05 );
		Stop;
	}
}
